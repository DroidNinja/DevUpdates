// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        compileSdkVersion = 29
        targetSdkVersion = 29
        minSdkVersion = 21
        buildToolsVersion = "29.0.3"
        versionCode = 1
        versionName = "1.0"
        kotlin_version = '1.3.72'
    }
    apply from: "$rootDir/gradleFiles/dependencies.gradle"
    addRepos(repositories)

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0-rc01'
        classpath 'com.google.gms:google-services:4.3.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$versions.kotinVersion"
        classpath 'com.google.firebase:perf-plugin:1.3.1'
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "3.13.0"
}


allprojects {
    addRepos(repositories)

    afterEvaluate {
        if (project.plugins.hasPlugin("kotlin-kapt")) {
            kapt {
                javacOptions {
                    option("-Xmaxerrs", 1000)
                }
            }
        }
    }
}

spotless {
    kotlin {
        target "**/*.kt"
        ktlint("0.29.0")
    }
    format 'misc', {
        target '**/*.md', '**/.gitignore'

        trimTrailingWhitespace()
        indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
        endWithNewline()
    }
    java {
        target "**/src/main/**/com/freecharge/*.java"
        googleJavaFormat()
        removeUnusedImports()
    }
}

project.ext.getBuildProperty = { property, defaultValue ->
    def value = project.getProperties().get(property)
    return value != null ? value : defaultValue
}
